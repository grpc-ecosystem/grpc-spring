buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
        maven { url 'http://repo.spring.io/plugins-release' }
        maven { url "https://plugins.gradle.org/m2/" }
    }
    ext {
        projectVersion = '1.1.0.RELEASE'

        grpcVersion = '1.2.0'
        protobufVersion = '3.2.0'
        protobufGradlePluginVersion = '0.8.0'

        springBootVersion = '1.4.5.RELEASE'
        springSleuthVersion = '1.1.3.RELEASE'
        springCloudVersion = 'Camden.SR6'
        springCloudConsulVersion = '1.1.3.RELEASE'
        springCloudEurekaVersion = '1.2.6.RELEASE'
    }
    dependencies {
        classpath "io.spring.gradle:dependency-management-plugin:0.6.1.RELEASE"
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.3'
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: "io.spring.dependency-management"

    compileJava {
        sourceCompatibility = JavaVersion.VERSION_1_7
        targetCompatibility = JavaVersion.VERSION_1_7
        options.encoding = 'UTF-8'
    }

    compileJava.options*.compilerArgs = [
            "-Xlint:serial", "-Xlint:varargs", "-Xlint:classfile", "-Xlint:dep-ann",
            "-Xlint:divzero", "-Xlint:empty", "-Xlint:finally", "-Xlint:overrides",
            "-Xlint:path", "-Xlint:static", "-Xlint:try", "-Xlint:fallthrough",
            "-Xlint:deprecation", "-Xlint:unchecked", "-Xlint:-options"
    ]

    // Enable Checkstyle
    if (null != System.getenv('ENABLE_CHECKSTYLE')) {
        apply plugin: 'checkstyle'
        dependencies {
            checkstyle 'com.puppycrawl.tools:checkstyle:6.15'
        }
    }

    tasks.withType(Checkstyle) {
        outputs.upToDateWhen { false }
        configFile = new File(rootDir, "checkstyle-google.xml")

        include 'net/devh/**/*'
    }

    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
        maven { url 'http://repo.spring.io/plugins-release' }
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies{
        compileOnly('org.projectlombok:lombok:1.16.6')
    }
    buildscript {
        repositories {
            mavenLocal()
            mavenCentral()
            jcenter()
            maven { url 'http://repo.spring.io/plugins-release' }
            maven { url "https://plugins.gradle.org/m2/" }
        }
    }
}

apply from: './deploy.gradle'

ext {
    hasJar = false
}

group = "net.devh"
version = "${projectVersion}"

dependencies {
    compile project(':grpc-server-spring-boot-starter')
    compile project(':grpc-client-spring-boot-starter')
}