import java.time.OffsetDateTime
import java.time.format.DateTimeFormatter

buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
        maven { url 'http://repo.spring.io/plugins-release' }
        maven { url "https://plugins.gradle.org/m2/" }
    }
    ext {
        projectVersion = '2.2.0.RELEASE'

        grpcVersion = '1.17.1'
        protobufVersion = '3.6.1'
        protobufGradlePluginVersion = '0.8.7'
        nettyTCNativeVersion = '2.0.17.Final' // Needs to be compatbile with the 'grpcVersion'

        springBootVersion = '2.0.7.RELEASE'
        springCloudVersion = 'Finchley.SR2'
        braveInstrumentationGrpc = '5.5.0'
        jUnitVersion = '5.3.1'

        lombokVersion = '1.18.4'
    }
    dependencies {
        classpath "io.spring.gradle:dependency-management-plugin:1.0.6.RELEASE"
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
        classpath "com.diffplug.spotless:spotless-plugin-gradle:3.16.0"
    }
}

plugins {
    id "net.nemerosa.versioning" version "2.8.2"
}

wrapper {
    gradleVersion = '5.0'
}

def buildTimeAndDate = OffsetDateTime.now()

ext {
    buildDate = DateTimeFormatter.ISO_LOCAL_DATE.format(buildTimeAndDate)
    buildTime = DateTimeFormatter.ofPattern('HH:mm:ss.SSSZ').format(buildTimeAndDate)
    buildRevision = versioning.info.commit
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'eclipse'
    apply plugin: "io.spring.dependency-management"
    apply plugin: 'com.diffplug.gradle.spotless'

    compileJava {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
        options.encoding = 'UTF-8'
    }

    compileJava.options*.compilerArgs = [
            "-Xlint:all", "-Xlint:-cast", "-Xlint:-processing"
    ]

    spotless {
        java {
            licenseHeaderFile rootProject.file('extra/spotless/mit-license.java')
            removeUnusedImports()
            importOrderFile rootProject.file('extra/eclipse/eclipse.importorder')
            eclipse().configFile rootProject.file('extra/eclipse/eclipse-formatter.xml')
        }
        format("misc") {
            target("**/*.gradle", "**/*.md", "**/*.yml")
            trimTrailingWhitespace()
            endWithNewline()
        }
    }

    // Enable Checkstyle
    if (null != System.getenv('ENABLE_CHECKSTYLE')) {
        apply plugin: 'checkstyle'
        checkstyle {
            toolVersion '8.14'
            configFile new File(rootDir, "extra/checkstyle/checkstyle.xml")
            showViolations true
        }
    }

    // Copy LICENSE
    tasks.withType(Jar) {
        from(project.rootDir) {
            include 'LICENSE'
            into 'META-INF'
        }
    }

    // Generate MANIFEST.MF
    jar {
        manifest {
            attributes(
                    'Created-By': "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})".toString(),
                    'Built-By': "travis",
                    'Build-Date': buildDate,
                    'Build-Time': buildTime,
                    'Built-OS': "${System.properties['os.name']}",
                    'Build-Revision': buildRevision,
                    'Specification-Title': project.name,
                    'Specification-Version': "${projectVersion}",
                    'Specification-Vendor': 'Michael Zhang',
                    'Implementation-Title': project.name,
                    'Implementation-Version': "${projectVersion}",
                    'Implementation-Vendor': 'Michael Zhang'
            )
        }
    }

    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
        maven { url 'http://repo.spring.io/plugins-release' }
        maven { url "https://plugins.gradle.org/m2/" }
        maven { url 'https://repo.spring.io/libs-milestone' }
    }

    dependencies {
        compileOnly("org.projectlombok:lombok:${lombokVersion}")
        testCompileOnly("org.projectlombok:lombok:${lombokVersion}")
        annotationProcessor("org.projectlombok:lombok:${lombokVersion}")
        testAnnotationProcessor("org.projectlombok:lombok:${lombokVersion}")
    }

    buildscript {
        repositories {
            mavenLocal()
            mavenCentral()
            jcenter()
            maven { url 'http://repo.spring.io/plugins-release' }
            maven { url "https://plugins.gradle.org/m2/" }
        }

        dependencyManagement {
            imports {
                mavenBom "org.springframework.boot:spring-boot-starter-parent:${springBootVersion}"
                mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
            }
        }

        ext {
            springFrameworkVersion = dependencyManagement.importedProperties['spring.version']
            springSecurityVersion = dependencyManagement.importedProperties['spring-security.version']
        }
    }

    tasks.withType(Javadoc) {
        def links = [
                'https://docs.oracle.com/javase/8/docs/api/',
                'https://grpc.io/grpc-java/javadoc/',
                'https://docs.spring.io/spring-framework/docs/' + springFrameworkVersion + '/javadoc-api/',
                'https://docs.spring.io/spring-security/site/docs/' + springSecurityVersion + '/api/'
            ];
        options.setLinks(links)
    }

}

apply from: './deploy.gradle'

group = "net.devh"
version = "${projectVersion}"

dependencies {
    compile project(':grpc-server-spring-boot-starter')
    compile project(':grpc-client-spring-boot-starter')
    testCompile project(':tests')
}
