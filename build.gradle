buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
        maven { url 'http://repo.spring.io/plugins-release' }
        maven { url "https://plugins.gradle.org/m2/" }
    }
    ext {
        projectVersion = '1.0.1.RELEASE'

        grpcVersion = '1.1.2'
        protobufVersion = '3.2.0'
        protobufGradlePluginVersion = '0.8.0'

        springBootVersion = '1.4.2.RELEASE'
        springSleuthVersion = '1.1.0.RELEASE'
        springCloudVersion = 'Camden.SR3'
        springCloudConsulVersion = '1.1.2.RELEASE'
        springCloudEurekaVersion = '1.2.3.RELEASE'
    }
    dependencies {
        classpath "io.spring.gradle:dependency-management-plugin:0.6.1.RELEASE"
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.3'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: "io.spring.dependency-management"

    compileJava {
        sourceCompatibility = JavaVersion.VERSION_1_7
        targetCompatibility = JavaVersion.VERSION_1_7
        options.encoding = 'UTF-8'
    }

    compileJava.options*.compilerArgs = [
            "-Xlint:serial", "-Xlint:varargs", "-Xlint:classfile", "-Xlint:dep-ann",
            "-Xlint:divzero", "-Xlint:empty", "-Xlint:finally", "-Xlint:overrides",
            "-Xlint:path", "-Xlint:static", "-Xlint:try", "-Xlint:fallthrough",
            "-Xlint:deprecation", "-Xlint:unchecked", "-Xlint:-options"
    ]

    // Enable Checkstyle
    if (null != System.getenv('ENABLE_CHECKSTYLE')) {
        apply plugin: 'checkstyle'
        dependencies {
            checkstyle 'com.puppycrawl.tools:checkstyle:6.15'
        }
    }

    tasks.withType(Checkstyle) {
        outputs.upToDateWhen { false }
        configFile = new File(rootDir, "checkstyle-google.xml")

        include 'net/devh/**/*'
    }

    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
        maven { url 'http://repo.spring.io/plugins-release' }
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies{
        compileOnly('org.projectlombok:lombok:1.16.6')
    }
    buildscript {
        repositories {
            mavenLocal()
            mavenCentral()
        }
    }
}