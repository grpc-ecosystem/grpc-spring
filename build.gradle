import java.time.OffsetDateTime
import java.time.format.DateTimeFormatter
import io.franzbecker.gradle.lombok.task.DelombokTask

buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
        maven { url 'http://repo.spring.io/plugins-release' }
        maven { url "https://plugins.gradle.org/m2/" }
    }
    ext {
        projectVersion = '2.5.0-SNAPSHOT'

        grpcVersion = '1.21.0'
        protobufVersion = '3.8.0'
        protobufGradlePluginVersion = '0.8.8'

        springBootVersion = '2.1.6.RELEASE'
        springCloudVersion = 'Greenwich.SR2'
        springCloudAlibabaNacosVersion = '0.9.0.RELEASE'
        braveInstrumentationGrpc = '5.6.3'

        lombokVersion = '1.18.8'
    }
    dependencies {
        classpath "io.spring.gradle:dependency-management-plugin:1.0.7.RELEASE"
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
        classpath "com.diffplug.spotless:spotless-plugin-gradle:3.23.0"
    }
}

plugins {
    id "net.nemerosa.versioning" version "2.8.2"
    id "io.franzbecker.gradle-lombok" version "3.1.0" apply false
    id "com.gradle.build-scan" version "2.2.1"
    id "com.github.ben-manes.versions" version "0.21.0" // gradle dependencyUpdates (Takes quite some time)
}

buildScan {
    termsOfServiceUrl = "https://gradle.com/terms-of-service"
    termsOfServiceAgree = "yes"
}

wrapper {
    gradleVersion = '5.0'
}

def buildTimeAndDate = OffsetDateTime.now()

ext {
    buildDate = DateTimeFormatter.ISO_LOCAL_DATE.format(buildTimeAndDate)
    buildTime = DateTimeFormatter.ofPattern('HH:mm:ss.SSSZ').format(buildTimeAndDate)
    buildRevision = versioning.info.commit
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'eclipse'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'com.diffplug.gradle.spotless'
    apply plugin: 'io.franzbecker.gradle-lombok'

    compileJava {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
        options.encoding = 'UTF-8'
    }

    compileJava.options*.compilerArgs = [
            "-Xlint:all", "-Xlint:-processing"
    ]

    spotless {
        java {
            licenseHeaderFile rootProject.file('extra/spotless/mit-license.java')
            removeUnusedImports()
            importOrderFile rootProject.file('extra/eclipse/eclipse.importorder')
            eclipse().configFile rootProject.file('extra/eclipse/eclipse-formatter.xml')
        }
        format("misc") {
            target("**/*.gradle", "**/*.md", "**/*.yml")
            trimTrailingWhitespace()
            endWithNewline()
        }
    }

    // Copy LICENSE
    tasks.withType(Jar) {
        from(project.rootDir) {
            include 'LICENSE'
            into 'META-INF'
        }
    }

    // Generate MANIFEST.MF
    jar {
        manifest {
            attributes(
                    'Created-By': "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})".toString(),
                    'Built-By': "travis",
                    'Build-Date': buildDate,
                    'Build-Time': buildTime,
                    'Built-OS': "${System.properties['os.name']}",
                    'Build-Revision': buildRevision,
                    'Specification-Title': project.name,
                    'Specification-Version': "${projectVersion}",
                    'Specification-Vendor': 'Michael Zhang',
                    'Implementation-Title': project.name,
                    'Implementation-Version': "${projectVersion}",
                    'Implementation-Vendor': 'Michael Zhang'
            )
        }
    }

    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
        maven { url 'http://repo.spring.io/plugins-release' }
        maven { url "https://plugins.gradle.org/m2/" }
        maven { url 'https://repo.spring.io/libs-milestone' }
    }

    buildscript {
        repositories {
            mavenLocal()
            mavenCentral()
            jcenter()
            maven { url 'http://repo.spring.io/plugins-release' }
            maven { url "https://plugins.gradle.org/m2/" }
        }
    }
}

Project commonProject = project(':grpc-common-spring-boot');

String javaAPIdoc;
if (JavaVersion.current().isJava9Compatible()) {
    javaAPIdoc = 'https://docs.oracle.com/en/java/javase/11/docs/api';
} else {
    javaAPIdoc = 'https://docs.oracle.com/javase/8/docs/api/';
}

allprojects { project ->
   buildscript {
        dependencyManagement {
            imports {
                mavenBom "io.grpc:grpc-bom:${grpcVersion}"
                mavenBom "com.google.protobuf:protobuf-bom:${protobufVersion}"
                mavenBom "org.springframework.boot:spring-boot-starter-parent:${springBootVersion}"
                mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
            }
        }

        ext {
            micrometerVersion = dependencyManagement.importedProperties['micrometer.version']
            springFrameworkVersion = dependencyManagement.importedProperties['spring-framework.version']
            springSecurityVersion = dependencyManagement.importedProperties['spring-security.version']
            springCloudCommonsVersion = dependencyManagement.importedProperties['spring-cloud-commons.version']
        }
    }
    if (project.name == 'grpc-common-spring-boot' || project.name == 'grpc-client-spring-boot-autoconfigure' || project.name == 'grpc-server-spring-boot-autoconfigure') {
        // Properly generate javadocs for the important projects

        task delombok(type: DelombokTask, dependsOn: compileJava) {
            ext.outputDir = file("$buildDir/delombok")
            outputs.dir(outputDir)
            sourceSets.main.java.srcDirs.each {
                inputs.dir(it)
                args(it, "-d", outputDir)
            }
        }

        java {
            registerFeature('optionalSupport') {
                usingSourceSet(sourceSets.main)
            }
        }

        // Javadoc Task
        javadoc {
            dependsOn delombok
            source = delombok.outputDir
            failOnError = false
            options.jFlags('-Dhttp.agent=gradle-javadoc') // Required for javadoc.io
            if (project.name != 'grpc-common-spring-boot') {
                options.linksOffline('https://static.javadoc.io/net.devh/grpc-common-spring-boot/' + projectVersion, commonProject.buildDir.getPath() + '/docs/javadoc')
            }
            options.links = [
                    javaAPIdoc,
                    'https://grpc.io/grpc-java/javadoc/',
                    'https://static.javadoc.io/io.micrometer/micrometer-core/' + micrometerVersion + '/',
                    'https://docs.spring.io/spring-framework/docs/' + springFrameworkVersion + '/javadoc-api/',
                    'https://docs.spring.io/spring-security/site/docs/' + springSecurityVersion + '/api/',
                    'https://docs.spring.io/spring-boot/docs/' + springBootVersion + '/api/',
                    'https://static.javadoc.io/org.springframework.cloud/spring-cloud-commons/' + springCloudCommonsVersion + '/',
                    // 'https://static.javadoc.io/io.zipkin.brave/brave/' + braveInstrumentationGrpc + '/', // Requires javadoc 11
                    // 'https://static.javadoc.io/io.zipkin.brave/brave-instrumentation-grpc/' + braveInstrumentationGrpc + '/', // Requires javadoc 11
                    'https://google.github.io/guava/releases/26.0-android/api/docs/'
            ]
        }
    }
}

apply from: './deploy.gradle'

group = "net.devh"
version = "${projectVersion}"

dependencies {
    compile project(':grpc-server-spring-boot-starter')
    compile project(':grpc-client-spring-boot-starter')
    testCompile project(':tests')
}
